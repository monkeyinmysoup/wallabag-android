
apply plugin: 'android'
apply plugin: 'android-apt'

def gitSha() {
    return 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}

apt {
    arguments {
        // androidManifestFile 'path_to_AndroidManifest_file'
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.processResources.manifestFile
    }
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"
    final PROVIDER_DEBUG = "com.pixplicity.wallabag.debug.provider"
    final PROVIDER_RELEASE = "com.pixplicity.wallabag.provider"

    // Fixes PopupMenu giving segfaults on 2.3.5
    aaptOptions.useAaptPngCruncher = true

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    lintOptions {
        //checkReleaseBuilds false
        abortOnError false
    }

    defaultConfig {
        applicationId "com.pixplicity.wallabag"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }

    signingConfigs {
        release {
            storeFile file("../keystore.keystore")
            storePassword ""
            keyAlias "wallabag.release"
            keyPassword ""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            debuggable false
            runProguard true
            jniDebugBuild false
            zipAlign true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            buildConfigField "String", "PROVIDER_AUTHORITY", "\"${PROVIDER_RELEASE}\""
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            buildConfigField "String", "PROVIDER_AUTHORITY", "\"${PROVIDER_DEBUG}\""
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'http://pixplicity.com:8081/nexus/content/repositories/releases/' }
}

dependencies {
    apt 'org.androidannotations:androidannotations:3.0'
    compile 'com.android.support:support-v4:+'
    compile files('libs/json-smart-1.2.jar')
    compile files('libs/jsonrpc2-base-1.35.jar')
    compile files('libs/jsonrpc2-client-1.14.4.jar')
    compile 'com.pixplicity.easyprefs:library:1.+'
    compile 'com.pixplicity:font.text.utils.library:1.3'
    compile 'nl.qbusict:cupboard:2.0.1'
    compile 'com.squareup.picasso:picasso:2.2.0'
    compile 'com.github.castorflex.smoothprogressbar:library:0.5.1'
}
